/*******************************************************************************
 * Copyright (c) 2022, 2025 Primetals Technologies Austria GmbH
 *                          Martin Erich Jobst
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *   Hesam Rezaee
 *       - initial API and implementation and/or initial documentation
 *   Martin Jobst
 *       - add ST source as top-level element
 *       - add package declaration and imports
 *       - update grammar for GLOBALCONSTANTS element
 *******************************************************************************/
grammar org.eclipse.fordiac.ide.globalconstantseditor.GlobalConstants with org.eclipse.fordiac.ide.structuredtextcore.STCore

import "http://www.eclipse.org/fordiac/ide/globalconstantseditor/GlobalConstants"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "org.eclipse.fordiac.ide.model.datatype" as datatype
import "org.eclipse.fordiac.ide.model.libraryElement" as libraryElement
import "http://www.eclipse.org/fordiac/ide/structuredtextcore/STCore" as stcore

STGlobalConstsSource returns stcore::STSource:
	{STGlobalConstsSource}
	('PACKAGE' name=QualifiedName ';')? // package declaration (optional)
	imports+=STImport* // imports (optional)
	constants=STGlobalConstants?;

STGlobalConstants returns STGlobalConstants:
	{STGlobalConstants}
	'GLOBALCONSTANTS' name=ID
	elements+=STVarGlobalDeclarationBlock*
	'END_GLOBALCONSTANTS';

STVarGlobalDeclarationBlock returns STVarGlobalDeclarationBlock:
	{STVarGlobalDeclarationBlock}
	'VAR_GLOBAL' (constant?='CONSTANT')?
	varDeclarations+=STVarDeclaration*
	'END_VAR';
